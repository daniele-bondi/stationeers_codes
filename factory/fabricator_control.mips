define RequestThreshold 100

alias fabricator d0
alias quantityControlSwitch d1
alias quantityControlDial d2
alias stacker d3
alias receivedRxMem d4
alias display d5

alias lastInput r0
alias lastExpCount r1
alias currExpCount r2
alias quantityControlOn r3
alias quantity r4
alias request r5
alias x r6
alias y r7

s fabricator On 1
s fabricator ClearMemory 1
s stacker On 1
move quantity 500
s stacker Setting quantity
move lastInput 0
s display On 1
jal updateRequests

main:
s display Setting sp
l x fabricator On
bnez x checkInput
s stacker Activate 1
s fabricator On 1
checkInput:
ls x fabricator 0 OccupantHash
beq x lastInput writeRx # skip if no change
move lastInput x
beqz x writeRx # skip if change is to no input
push x # push recieved item hash
jal updateRequests
writeRx:
beqz sp checkExportChange # skip if nothing on stack
l x receivedRxMem Setting
bnez x checkExportChange # skip if Rx occupied
pop x
s receivedRxMem Setting x
checkExportChange:
l currExpCount fabricator ExportCount
beq currExpCount lastExpCount main
# skip if nothing new crafted
move lastExpCount currExpCount
jal updateRequests
updateQC:
l x quantityControlSwitch Setting
beq x quantityControlOn QC # skip if no change
brnez x 3 # turned off
s stacker Setting 500
jr 3
s fabricator ClearMemory 1 # turned on
s stacker Setting quantity
move quantityControlOn x
QC:
#s display Setting currExpCount
beqz quantityControlOn main # skip if off
l y quantityControlDial Setting
breq y quantity 3 # set stacker if quantity changed
move quantity y
s stacker Setting quantity
blt currExpCount quantity main # skip if not reached target
s fabricator ClearMemory 1
s fabricator On 0
yield
s fabricator On 1
j main

updateRequests: # check reagent stocks, update request
push ra
move request 0
move y 1
lr x fabricator Contents Copper
jal addRequestBit
lr x fabricator Contents Gold
jal addRequestBit
lr x fabricator Contents Iron
jal addRequestBit
lr x fabricator Contents Lead
jal addRequestBit
lr x fabricator Contents Nickel
jal addRequestBit
lr x fabricator Contents Silicon
jal addRequestBit
lr x fabricator Contents Silver
jal addRequestBit
lr x fabricator Contents Constantan
jal addRequestBit
lr x fabricator Contents Electrum
jal addRequestBit
lr x fabricator Contents Invar
jal addRequestBit
lr x fabricator Contents Solder
jal addRequestBit
lr x fabricator Contents Steel
jal addRequestBit
lr x fabricator Contents Astroloy
jal addRequestBit
lr x fabricator Contents Hastelloy
jal addRequestBit
lr x fabricator Contents Inconel
jal addRequestBit
lr x fabricator Contents Stellite
jal addRequestBit
lr x fabricator Contents Waspaloy
jal addRequestBit
s db Setting request
pop ra
j ra

addRequestBit:
slt x x RequestThreshold
mul x y x
add request x request
mul y 2 y
j ra