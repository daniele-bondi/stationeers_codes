# Factory - Fabricator Control
define RequestThreshold 100
define ReceivedWaitDuration 240

alias fabricator d0
alias qcSwitch d1
alias qcDial d2
alias stacker d3
alias ledStacker d4 # stacker quantity
alias ledQC d5 # QC count

alias exportCount r0
alias qcOn r1
alias quantity r2
alias request r3
alias receivedWait r4
alias x r5
alias y r6

move qcOn 0
move quantity 500
s fabricator On 1
s fabricator ClearMemory 1
s stacker On 1
s stacker Setting quantity
s ledStacker On 1
s ledStacker Color 4 # red
s ledQC Color 2 # green

main:
ls x stacker 2 Quantity
s ledStacker Setting x # how much in stacker
updateRequests:
# check reagent stocks, update requests
move request 0
move y 1
lr x fabricator Contents Copper
jal addRequestBit
lr x fabricator Contents Gold
jal addRequestBit
lr x fabricator Contents Iron
jal addRequestBit
lr x fabricator Contents Lead
jal addRequestBit
lr x fabricator Contents Nickel
jal addRequestBit
lr x fabricator Contents Silicon
jal addRequestBit
lr x fabricator Contents Silver
jal addRequestBit
lr x fabricator Contents Constantan
jal addRequestBit
lr x fabricator Contents Electrum
jal addRequestBit
lr x fabricator Contents Invar
jal addRequestBit
lr x fabricator Contents Solder
jal addRequestBit
lr x fabricator Contents Steel
jal addRequestBit
lr x fabricator Contents Astroloy
jal addRequestBit
lr x fabricator Contents Hastelloy
jal addRequestBit
lr x fabricator Contents Inconel
jal addRequestBit
lr x fabricator Contents Stellite
jal addRequestBit
lr x fabricator Contents Waspaloy
jal addRequestBit
s db Setting request # down R request for sorter

emptyStacker:
l x fabricator On
bnez x updateQCOn
s stacker Activate 1
s fabricator On 1

updateQCOn:
l x qcSwitch Setting
beq x qcOn updateQCQuantity # skip if unchanged
breq x 1 3 # branch by turned on/off
s stacker Setting 500 # x = 1, turned off
jr 2
s fabricator ClearMemory 1 # x = 0, turned on
s ledQC On x
move qcOn x

updateQCQuantity:
l x qcDial Setting
beq x quantity QC # skip if quantity unchanged
move quantity x
s stacker Setting quantity

QC:
beqz qcOn main # skip if off
l x fabricator ExportCount
s ledQC Setting x # how much exported
brlt x quantity 5 # skip if not reached target
s fabricator ClearMemory 1
s fabricator On 0
yield
s fabricator On 1
j main

addRequestBit:
slt x x RequestThreshold
mul x y x
add request x request
mul y 2 y
j ra