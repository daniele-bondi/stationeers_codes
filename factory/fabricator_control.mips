# Factory - Fabricator Control
define RequestThreshold 100

alias fabricator d0
alias receivedIC d1
alias qcSwitch d2
alias qcDial d3
alias stacker d4
alias display d5

alias exportCount r0
alias qcOn r1
alias quantity r2
alias request r3
alias x r4
alias y r5

s fabricator On 1
s fabricator ClearMemory 1
s stacker On 1
move quantity 500
s stacker Setting quantity
s display On 1

main:
updateRequests:
# check reagent stocks, update requests
move request 0
move y 1
lr x fabricator Contents Copper
jal addRequestBit
lr x fabricator Contents Gold
jal addRequestBit
lr x fabricator Contents Iron
jal addRequestBit
lr x fabricator Contents Lead
jal addRequestBit
lr x fabricator Contents Nickel
jal addRequestBit
lr x fabricator Contents Silicon
jal addRequestBit
lr x fabricator Contents Silver
jal addRequestBit
lr x fabricator Contents Constantan
jal addRequestBit
lr x fabricator Contents Electrum
jal addRequestBit
lr x fabricator Contents Invar
jal addRequestBit
lr x fabricator Contents Solder
jal addRequestBit
lr x fabricator Contents Steel
jal addRequestBit
lr x fabricator Contents Astroloy
jal addRequestBit
lr x fabricator Contents Hastelloy
jal addRequestBit
lr x fabricator Contents Inconel
jal addRequestBit
lr x fabricator Contents Stellite
jal addRequestBit
lr x fabricator Contents Waspaloy
jal addRequestBit
s db Setting request # down R request for sorter
#s receivedIC Setting 0 # clear to send received RX
onSwitchEmptyStacker:
l x fabricator On
bnez x updateQC
s stacker Activate 1
s fabricator On 1
updateQC:
l x qcSwitch Setting
beq x qcOn QC # skip if no change
l y qcDial Setting
brnez x 3 # branch by turned on/off
s stacker Setting 500 # turned off
jr 2
s fabricator ClearMemory 1 # turned on
move qcOn x
beqz qcOn QC # skip if QC off
beq y quantity QC # skip if quantity unchanged
move quantity y
s stacker Setting quantity
QC:
l x fabricator ExportCount
s display Setting x
beqz qcOn main # skip if off
blt x quantity main # skip if not reached target
s fabricator ClearMemory 1
s fabricator On 0
yield
s fabricator On 1
j main

addRequestBit:
slt x x RequestThreshold
mul x y x
add request x request
mul y 2 y
j ra