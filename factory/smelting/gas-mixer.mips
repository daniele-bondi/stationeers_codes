# Factory - CO2 Furnace Gas Mixer
alias Furnace d0
alias HAnalyzer d1
alias CAnalyzer d2
alias IAnalyzer d3
alias InputDumpPump d4
alias Mixer d5

define MsgPTarget 1
define MsgTTarget 2
define MsgStart 3

define R 8.31446261815324
define InputVolume 1500 # 15 pipes * 100 L
define FurnaceVolume 1000

alias pT r0
alias tT r1
alias nT r2
alias nI r3
alias nH r4
alias nC r5
alias nF r6
alias x r7
alias y r8

move tT 0
move pT 0
s db Setting 0
s Furnace SettingInput 0
s Furnace SettingOutput 0

main:
yield
l x db Setting
brne x MsgPTarget 4 # receive pressure target
jal waitReceive
move pT x
jal calculateMolesTarget
brne x MsgTTarget 4 # receive temperature target
jal waitReceive
move tT x
jal calculateMolesTarget
bne x MsgStart main # received start command
calculateMolesInput:
l x Furnace Temperature # x = tF
l nF Furnace TotalMoles
sub nI nT nF # nI = (nT-nF)
mul y tT nT # y = tI...
mul x x nF
sub y y x
div y y nI # ... = (tI = (tT*nT-tF*nF)/nI)
calculateMolesHandC:
l x CAnalyzer Temperature # x = tC
sub nH y x # nH...
mul nH nH nI
l y HAnalyzer Temperature # y = tH
sub x y x
div nH nH x # ... = nI*(tI-tC)/(tH-tC)
sub nC nI nH # nC = nI-nH
checkMolesInput:
min x nH nC # min(nH, nC)
bgez x calculateMixerRatio # skip if positive
removeExcess:
jal dump # dump furnace gas
add nF nF x
div nF nF InputVolume
s InputDumpPump On 1 # remove excess...
yield
l y IAnalyzer TotalMoles
brgt y nF -2
s InputDumpPump On 0
jal fill # refill with excess removed
j calculateMolesInput # recalculate
calculateMixerRatio:
l x HAnalyzer TotalMoles
div x nH x # x = (rH : moles H to moles in pipe)
l y CAnalyzer TotalMoles
div y nC y # y = (rC : moles C to moles in pipe)
add y x y # y = rH+rC
div x x y # x = rH/(rH+rC) : mixer setting
composeInput:
s Mixer Setting x
s Mixer On 1
yield
div y nI InputVolume # target moles in single pipe
l x IAnalyzer TotalMoles # loop...
yield
brlt x y -2 # ...until moles achieved
s Mixer On 0
jal fill
s db Setting 0
j main
calculateMolesTarget: #nF
mul nT pT FurnaceVolume # nT ...
div nT nT R
div nT nT tT # ... = (pT*vT)/(R*tT)
j ra
fill:
s Furnace SettingInput 100
yield
l x IAnalyzer Pressure
brgtz x -2
s Furnace SettingInput 0
j ra
dump:
s Furnace SettingOutput 100
yield
l x Furnace Pressure
brgtz x -2
s Furnace SettingOutput 0
j ra
waitReceive:
s db Setting 0
yield
l x db Setting
breqz x -2
s db Setting 0
j ra