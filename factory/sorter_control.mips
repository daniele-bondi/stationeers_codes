# Factory - Sorter
alias sorter d0
alias requestDownLMem d1
alias requestDownRMem d2
alias requestUpMem d3

alias requestDownL r0
alias requestDownR r1
alias requestUp r2
alias wordOr r2 # same as request up
alias occupantHash r3
alias wordA r4
alias output r5
alias wordB r6
alias needUpdate r7
alias bitOr r8
alias bitA r9
alias bitB r10
alias i r11
alias x r12

move requestDownL 0
move requestDownR 0
move needUpdate 0

s sorter On 1
s sorter Mode 2
s db Setting 0

main:
sortOccupant:
ls occupantHash sorter 0 OccupantHash # skip empty
beqz occupantHash checkDownLeft
jal getOutput
s sorter Output output
checkDownLeft:
bdns requestDownLMem checkDownRight # skip unset
l x requestDownLMem Setting # x = down left mem
beq x requestDownL checkDownRight # skip unchanged
move requestDownL x
move needUpdate 1 # trigger update
checkDownRight:
bdns requestDownRMem updateRequest # skip if unset
l x requestDownRMem Setting # x = down right mem
beq x requestDownR updateRequest # skip unchanged
move requestDownR x
move needUpdate 1 # trigger update
updateRequest:
beqz needUpdate main # skip if no update triggered
move wordA requestDownL
move wordB requestDownR
jal bitwiseOr
s requestUpMem Setting requestUp
move needUpdate 0
j main

getOutput:
mul i 2 i # 1
beq occupantHash -404336834 extractBit # copper
mul i 2 i # 2
beq occupantHash 226410516 extractBit # gold
mul i 2 i # 4
beq occupantHash -1301215609 extractBit # iron
mul i 2 i # 8
beq occupantHash 2134647745 extractBit # lead
mul i 2 i # 16
beq occupantHash -1406385572 extractBit # nickel
mul i 2 i # 32
beq occupantHash -290196476 extractBit # silicon
mul i 2 i # 64
beq occupantHash -929742000 extractBit # silver
mul i 2 i # 128
beq occupantHash 1058547521 extractBit # constantan
mul i 2 i # 256
beq occupantHash 502280180 extractBit # electrum
mul i 2 i # 512
beq occupantHash -297990285 extractBit #invar
mul i 2 i # 1024
beq occupantHash -82508479 extractBit # solder
mul i 2 i # 2048
beq occupantHash -654790771 extractBit # steel
mul i 2 i # 4096
beq occupantHash 412924554 extractBit # astroloy
mul i 2 i # 8192
beq occupantHash 1579842814 extractBit # hastelloy
mul i 2 i # 16384
beq occupantHash -787796599 extractBit # inconel
mul i 2 i # 32768
beq occupantHash -1897868623 extractBit # stellite
mul i 2 i # 65536
beq occupantHash 156348098 extractBit # waspaloy
mul i 2 i # 131072
beq occupantHash 1724793494 extractBit # ore (coal)
mul i 2 i # 262144
beq occupantHash -983091249 extractBit # ore (cobalt)
move output 1
j ra # output = 1 (left) if not our item 
extractBit:
div x requestDownR i
trunc x x
mod output x 2
s db Setting output
j ra

bitwiseOr:
move wordOr 0
move i 1
bitwiseOrLoop:
mod bitA wordA 2 # get LS bit from word A
div wordA wordA 2 # right shift word A
trunc wordA wordA
mod bitB wordB 2 # get LS bit from word B
div wordB wordB 2 # right shift word B
trunc wordB wordB
or bitOr bitA bitB # or the bits
mul bitOr i bitOr # make bit a power of 2
add wordOr bitOr wordOr # add to result
mul i 2 i
bgtz wordA bitwiseOrLoop # loop if either word not 0
bgtz wordB bitwiseOrLoop
j ra

